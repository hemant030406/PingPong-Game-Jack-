class Ball{
    
    field int x,y,r,vx,vy;
    field int xi,yi,vxi,vyi;
    field int score1,score2;
    field Slider userSlider;
    field Slider AISlider;
    field Array velx;
    field Array vely;

    constructor Ball new(int ax, int ay, int ar, int avx, int avy){
        let userSlider = Slider.new(504,105,511,151);
        do userSlider.draw();

        let AISlider = Slider.new(0,105,7,151);
        do AISlider.draw();

        let velx = Array.new(7);
        let vely = Array.new(7);

        let velx[0] = 4;
        let vely[0] = 6;

        let velx[1] = -4;
        let vely[1] = -5;

        let velx[2] = 4;
        let vely[2] = -4;

        let velx[3] = -4;
        let vely[3] = -6;

        let velx[4] = -4;
        let vely[4] = 5;

        let velx[5] = 4;
        let vely[5] = -5;

        let velx[6] = -4;
        let vely[6] = -6;

        let xi = ax;
        let yi = ay;
        let vxi=avx;
        let vyi=avy;

        let x = ax;
        let y = ay;
        let r = ar;
        let vx = velx[0];
        let vy = vely[0];
        let score1 = 0;
        let score2 = 0;

        return this;
    }


    method void drawScoreboard(){
        do Output.printString("Player1: ");

        do Output.moveCursor(0,50);
        do Output.printString("Player2: ");

        do Screen.setColor(true);
        do Screen.drawLine(0,15,511,15);
        
        do incScore(0,9,0);
        do incScore(0,59,0);

        do Output.moveCursor(0,20);
        do Output.printString("Each Player has 6 Lives");

        do Output.moveCursor(2,20);
        do Output.printString("Press Space to Start");

        do Output.moveCursor(4,20);
        do Output.printString("Press p to Pause");

        do Output.moveCursor(6,20);
        do Output.printString("Press r to Resume");

        do Output.moveCursor(8,20);
        do Output.printString("Press q to Quit");

        do Sys.wait(5000);
        do Screen.setColor(false);
        do Screen.drawRectangle(160,0,345,14);
        do Screen.drawRectangle(160,22,320,100);

        return;
    }

    method void incScore(int i, int j, int score){
        do Output.moveCursor(i,j);
        do Output.printInt(score);

        return;
    }

    method void draw() {
        do Screen.setColor(true);
        do Screen.drawCircle(x,y,r);
        return;
    }

    method void erase() {
        do Screen.setColor(false);
        do Screen.drawCircle(x,y,r);
        return;
    }

    method void reset(int i){

        do erase();

        do userSlider.erase(userSlider.getx1(),userSlider.gety1(),userSlider.getx2(),userSlider.gety2());
        do AISlider.erase(AISlider.getx1(),AISlider.gety1(),AISlider.getx2(),AISlider.gety2());

        let userSlider = Slider.new(504,105,511,151);
        do userSlider.draw();

        let AISlider = Slider.new(0,105,7,151);
        do AISlider.draw();

        let x = xi;
        let y = yi;
        let vx=velx[i];
        let vy=vely[i];

        return;
    }
    
    method void dispose() {
      do userSlider.dispose();
      do AISlider.dispose();
      do Memory.deAlloc(this);
      return;
    }

    method void run(){

        var int key,count;
        var boolean pause,move,exit;
        let pause = false;
        let move = false;
        let exit = false;

        do drawScoreboard();

        while(~exit){

            let count = 0;

            while(~pause) {

                let key = Keyboard.keyPressed();

                if (key = 32){
                    let move = true;
                }


                while(move & ~pause){

                    let key = Keyboard.keyPressed();

                    do erase();
                    do userSlider.draw();
                    do AISlider.draw();

                    if(x > (511 - vx - r)){

                        if (((y - r) > userSlider.gety2()) | ((y + r) < userSlider.gety1())){

                            if (count < 6){
                                let count = count + 1;
                            }
                            else{
                                let count = 0;
                            }

                            do draw();
                            do Sys.wait(1000);
                            do reset(count);
                            let score1 = score1 + 1;
                            do incScore(0,9,score1);
                            let move = false;
                        }

                        else{
                            let vx=-vx;
                        }
                    }
                

                    if(y > (255 - vy - r)){
                        let vy=-vy;
                    }

                    if(x < (-vx + r)){
                        if (((y - r) > (AISlider.gety2())) | ((y + r) < (AISlider.gety1()))){

                            if (count < 6){
                                let count = count + 1;
                            }
                            else{
                                let count = 0;
                            }

                            do draw();
                            do Sys.wait(1000);
                            do reset(count);
                            let score2 = score2 + 1;
                            do incScore(0,59,score2);
                            let move = false;
                        }
                        else{
                            let vx=-vx;
                        }
                    }

                    if(y < (16 + (-vy + r))){
                        let vy=-vy;
                    }

                    let x = x + vx;
                    let y = y + vy;

                    do draw();

                    if(key=80) {
                        let pause = true;
                    }

                    if (key = 131){
                        do userSlider.moveUp(3);
                    }

                    if (key = 133){
                        do userSlider.moveDown(3);
                    }

                    if ((x<140) & (vx<0)){
                        if (vy>0){
                            if((y - AISlider.gety2()) > 7 ){
                                do AISlider.moveDown(6);
                            }
                            else{
                                do AISlider.moveDown(2);
                            }
                        }
                        else{
                            if((AISlider.gety1() - y) > 7 ){
                                do AISlider.moveUp(6);
                            }
                            else{
                                do AISlider.moveUp(2);
                            }
                        }
                    }

                    do Sys.wait(15);

                    if (score1 = 6) {
                        do erase();
                        do Output.moveCursor(11,27);
                        do Output.printString("YOU LOST!!!");

                        do Sys.wait(2000);
                        let pause = true;
                        let exit = true;
                    }

                    if(score2 = 6){
                        do erase();
                        do Output.moveCursor(11,27);
                        do Output.printString("YOU WON!!!");
                        
                        do Sys.wait(2000);
                        let pause = true;
                        let exit = true;
                    }

                }
            }

            let key = Keyboard.keyPressed();

            if(key = 82){
                let pause = false;
            }
            if (key = 81){
                let exit = true;
            }
        }

        return;
    }
}
